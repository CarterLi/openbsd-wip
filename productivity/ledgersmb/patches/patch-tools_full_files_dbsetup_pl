--- /dev/null	Tue Aug 27 17:43:51 2013
+++ tools/full_files_dbsetup_pl	Tue Aug 27 14:11:58 2013
@@ -0,0 +1,733 @@
+#!/usr/bin/perl
+
+# Script can be used by automated scripts, manually with options
+# or with an interactive process.
+
+our $VERSION = 2.8.0;
+
+use warnings;
+use strict;
+
+use File::Basename;
+use File::Spec;
+use File::Path qw(make_path);
+use Encode;
+use DBI;
+use DBD::Pg qw(:pg_types);
+use Getopt::Long;
+
+# Default variable values section
+my $editor              = $ENV{EDITOR} || "vi";
+my $owner               = 'ledgersmb';
+my $pass                = 'LEDGERSMB';
+my $host                = '127.0.0.1';
+my $port                = '5432';
+my $srcdir              = '/usr/local/share/ledgersmb/web';
+my $dstdir              = '/var/www/htdocs/ledgersmb';
+my $coa                 = "$dstdir/sql/coa/us/chart/General.sql";
+my $countrycode         = 'US';
+my $postgres_username   = 'postgres';
+my $postgres_db         = 'postgres';
+my $pgsql_contrib_dir   = '/usr/local/share/postgresql/extension';
+my $admin_firstname     = 'Default';
+my $admin_middlename    = 'NULL';
+my $admin_lastname      = 'Admin';
+my $admin_username      = 'admin';
+my $admin_password      = 'admin';
+my $httpd_conf_location = '/var/www/conf/modules';
+my $uid                 = `id -u`;
+my $gid                 = `id -g`;
+my $not_interactive     = 0;
+my $shell_quiet         = '>/dev/null 2>&1';
+my @contrib_scripts     = qw(pg_trgm tsearch2 tablefunc);
+my $web_server_user     = '';
+my $web_server_group    = '';
+my $cp_lsmb_conf_tmpl;
+my $postgres_password;
+my $company_name;
+my $gifi;
+my $no_postgres_pass;
+my $verbose;
+my $help;
+my $is_v9_1plus_aref;
+my $dbh;
+my $dbhc;
+my $dbn;
+my $sth;
+my $rc;
+my $rv;
+my $psql_cmd;
+my $cmd;
+my @languages;
+my @locale_language_results;
+my $language;
+my $code;
+my $description;
+
+# Get absolute tools directory
+my ( undef, $tooldir, undef ) = fileparse($0);
+$tooldir =~ s/\/$//x;
+$tooldir = File::Spec->rel2abs($tooldir);
+
+# Usage explanation section
+
+my $usage = qq{
+usage: $0 --company COMPANY --pgsql_contrib_dir DIR [option1 .. optionN]
+
+You will need to have permission to write the new ledgersmb-httpd.conf
+and the postgres password, if it is used.
+Unless you select --no_postgres_pass, you will be asked to enter the
+password of the '$postgres_username' user.
+
+This script creates a '$owner' superuser on the specified PostgreSQL host,
+if it does not exist.  Then it proceeds to load the LedgerSMB database
+structure and content, loading Chart of Accounts data and GIFI data
+as specified in the argument list.
+
+The interactive option will allow you to see a listing of the
+possible charts of accounts, country codes or GIFIs.
+
+Available options:
+ --not_interactive     Does not run interactively. Command line options only
+ --srcdir              The path where the sources for LedgerSMB are located
+                       [$srcdir]
+ --dstdir              The path where the sources will be located when invoked
+                       from the webserver [$dstdir]
+ --host                The PostgreSQL host to connect to (see 'man psql') [$host]
+ --port                The PostgreSQL port to connect to (see 'man psql') [$port]
+ --pgsql_contrib_dir   The directory with pg_trgm.sql, tablefunc.sql, tsearch2.sql
+                       are located (required for PostgreSQL version < 9.1.x)
+ --company             The name of the database to be created. Use lowercase [*]
+ --countrycode         The name of the 2 character country code. [$countrycode]
+ --owner               The name of the superuser which is to become owner of the
+                       company's database. Use lowercase [$owner]
+ --password            The password for the superuser owner of the company
+                       database. [$pass]
+ --coa                 The path locating the file to be used to load the
+                       Chart of Accounts data
+                       [$dstdir/sql/coa/us/chart/General.sql]
+ --gifi                The path locating the file to be used to load the
+                       GIFI data with the chart of accounts
+ --postgres_username   Postgres username [$postgres_username]
+ --postgres_password   Postgres user password. Defaults to none
+ --no_postgres_pass    No postgres password is needed
+ --postgres_db         Postgres database name. This is not related to the company
+                       database [$postgres_db]
+ --editor              Editor used to edit ledgersmb.conf [$editor]
+ --httpd_conf_location Location for the new ledgersmb-htpd.conf [$httpd_conf_location]
+ --web_server_user     User that web server will use for ledgersmb [$web_server_user]
+ --web_server_group    Group that web server will use for ledgersmb [$web_server_group]
+ --cp_lsmb_conf_tmpl   Copy ledgersmb.conf.template to ledgersmb.conf instead of editing
+ --verbose             Echoes the commands executed by the script during setup
+ --help                Shows this help screen
+
+ * These arguments don't have a default, but are required
+};
+
+GetOptions(
+    'help|?|h'                  => \$help,
+    'not_interactive|n'         => \$not_interactive,
+    'srcdir:s'                  => \$srcdir,
+    'dstdir:s'                  => \$dstdir,
+    'host:s'                    => \$host,
+    'port:i'                    => \$port,
+    'pgsql_contrib_dir|pc:s'    => \$pgsql_contrib_dir,
+    'company=s'                 => \$company_name,
+    'owner:s'                   => \$owner,
+    'password:s'                => \$pass,
+    'coa:s'                     => \$coa,
+    'gifi:s'                    => \$gifi,
+    'countrycode:s'             => \$countrycode,
+    'postgres_username|pu:s'    => \$postgres_username,
+    'postgres_password|pp:s'    => \$postgres_password,
+    'no_postgres_pass|nop'      => \$no_postgres_pass,
+    'postgres_db|pdb:s'         => \$postgres_db,
+    'editor|ed:s'               => \$editor,
+    'httpd_conf_location|hcl:s' => \$httpd_conf_location,
+    'web_server_user|wsu:s'     => \$web_server_user,
+    'web_server_group|wsg:s'    => \$web_server_group,
+    'cp_lsmb_conf_tmpl|c'       => \$cp_lsmb_conf_tmpl,
+    'verbose|v'                 => \$verbose,
+);
+if ($help) {
+    print $usage;
+    exit;
+}
+
+if ($not_interactive) {
+    if ( !$company_name ) {
+        print $usage;
+        print "\nmissing or empty --company option\n";
+        exit;
+    }
+
+    if ( ( !$postgres_password ) && ( !$no_postgres_pass ) ) {
+        print $usage;
+        print "\nPlease enter postgres password or select no_postgres_pass\n";
+        exit;
+    }
+
+    if ( !$pgsql_contrib_dir ) {
+        print "\n\nYou must specify a pgsql contrib directory!\n\n";
+        exit;
+    }
+
+    if ($verbose) {
+        $shell_quiet = '';
+    }
+
+    #   Copy source files into web site directories
+    make_path($dstdir);
+
+    if ( system("cp -R $srcdir/* $dstdir $shell_quiet") ) {
+        die "unable to cp -R $srcdir/* $dstdir $!\n";
+    }
+
+    chdir $dstdir or die "unable to change to $dstdir $!\n";
+
+}
+else {
+    interactive_setup();
+}
+
+# Get languages, which are inserted further below
+@locale_language_results
+    = `$tooldir/generate-language-table-contents.pl $dstdir/locale/po`;
+
+if ( !stat "/tmp/ledgersmb" ) {
+    mkdir "/tmp/ledgersmb" or die "unable to mkdir /tmp/ledgersmb $!\n";
+}
+
+if ( $web_server_user =~ /\w+/x ) {
+    $uid = getpwnam($web_server_user);
+}
+
+if ( $web_server_group =~ /\w+/x ) {
+    $gid = getgrnam($web_server_group);
+}
+
+# For debugging. Does no harm if left here.
+system("rm $dstdir/ledgersmb.conf $shell_quiet");
+
+if ($cp_lsmb_conf_tmpl) {
+    if (system(
+            "cp $dstdir/ledgersmb.conf.default $dstdir/ledgersmb.conf $shell_quiet"
+        )
+        )
+    {
+        die "Unable to ledgersmb.conf.default to ledgersmb.conf";
+    }
+}
+
+if ( !stat "$dstdir/ledgersmb.conf" ) {
+    setup_ledgersmb_conf($dstdir);
+}
+
+# This shell function returns 0 for success
+# Stops warnings in case of root uid/gid (which is 0)
+no warnings qw(uninitialized);
+if ( system("chown -R $uid:$gid $dstdir") ) {
+    if ($verbose) {
+        print
+            "\nWARNING:\nUnable to change ownership of $dstdir to \nuserid $uid and \ngroupid $gid\n";
+    }
+}
+use warnings qw(all);
+
+# ENVIRONMENT NECESSARY for psql
+$ENV{PGUSER}     = $owner;
+$ENV{PGPASSWORD} = $pass;
+$ENV{PGDATABASE} = $company_name;
+
+# Create ledgersmb_httpd.conf from default template
+create_ledgersmb_httpd_conf();
+
+# Connect to main database, not the company database
+if ($verbose) {
+    print "\nConnecting to database $postgres_db\n\n\n";
+}
+if ($postgres_password) {
+    $dbh = DBI->connect(
+        "DBI:Pg:dbname=$postgres_db;host=$host;port=$port",
+        $postgres_username,
+        $postgres_password,
+        { 'AutoCommit' => 1, 'RaiseError' => 1, pg_enable_utf8 => 1 }
+    ) or die "unable to connect to database $postgres_db. $DBI::errstr";
+}
+else {
+    $dbh = DBI->connect( "DBI:Pg:dbname=$postgres_db;host=$host;port=$port",
+        $postgres_username,
+        { 'AutoCommit' => 1, 'RaiseError' => 1, pg_enable_utf8 => 1 } )
+        or die "unable to connect to database $postgres_db. $DBI::errstr";
+}
+if ($verbose) {
+    print "\nConnected to database $postgres_db\n\n\n";
+}
+
+# Create owner role for company database
+$rv
+    = $dbh->do(
+    "CREATE ROLE $owner WITH SUPERUSER LOGIN NOINHERIT ENCRYPTED PASSWORD '$pass';"
+    ) or die "unable to CREATE ROLE $owner. $DBI::errstr";
+
+if ($verbose) {
+    print "username = $owner\n";
+    print "company_name = $company_name\n";
+    print "password = $pass\n";
+    print "login = $owner\n";
+}
+
+# Create company database
+$dbhc = DBI->connect('dbi:Pg:dbname=template1');
+
+$dbhc->{RaiseError} = 1;
+$dbhc->{AutoCommit} = 1;
+$dbn                = $dbhc->quote_identifier( $ENV{PGDATABASE} );
+$rc
+    = $dbhc->do(
+    "CREATE DATABASE $dbn WITH TEMPLATE template0 ENCODING 'UTF8'")
+    or die "Unable to create new database $company_name. $DBI::errstr";
+
+$is_v9_1plus_aref = $dbh->selectcol_arrayref("SELECT version();");
+
+# unpackaged is to bring older postgresql versions up to date
+# 9.1 or newer do not use unpackaged file
+if (   ( $$is_v9_1plus_aref[0] !~ /PostgreSQL 9\.[1-9]/x )
+    && ($pgsql_contrib_dir) )
+{
+    if ($verbose) {
+        print "$$is_v9_1plus_aref[0]\nPostgreSQL < 9.1.x\n";
+    }
+
+    # Add contrib extensions
+    for my $contrib (@contrib_scripts) {
+        if ( system("psql -f $pgsql_contrib_dir/$contrib $shell_quiet") ) {
+            die "Unable to process contrib extensions";
+        }
+    }
+}
+
+# Begin filling company database with table/roles/functions/etc
+if ( system("psql -f $dstdir/sql/Pg-database.sql $shell_quiet") ) {
+    die "Unable to process table/roles/functions/etc";
+}
+
+# load modules
+open( my $fh_loader, '<', "$dstdir/sql/modules/LOADORDER" )
+    or die "Unable to open $dstdir/sql/modules/LOADORDER $!";
+
+for my $mod (<$fh_loader>) {
+    chomp($mod);
+    $mod =~ s/#.*//x; # Remove comments
+    $mod =~ s/^\s*//x; # Remove leading spaces
+    $mod =~ s/\s*$//x; # Remove trailing spaces
+    next if $mod eq '';
+    if ($verbose) {
+        print "Loading module: $dstdir/sql/modules/$mod\n";
+    }
+    if ( system("psql -f $dstdir/sql/modules/$mod $shell_quiet") ) {
+        die "Unable to load module $mod";
+    }
+}
+
+close($fh_loader);
+
+if ($verbose) {
+    print "Processing company to create lsmb roles.";
+}
+
+open( my $fh_roles, '<', "$dstdir/sql/modules/Roles.sql" )
+    or die "Unable to open $dstdir/sql/modules/Roles.sql $!";
+open( my $fh_temp_roles, '>', "$dstdir/sql/temp_lsmb_roles.sql" )
+    or die "Unable to open $dstdir/sql/temp_lsmb_roles.sql $!";
+
+for my $line (<$fh_roles>) {
+    $line =~ s/<\?lsmb dbname \?>/$company_name/x; # Changes first part into $company_name
+    print {$fh_temp_roles} $line;
+}
+
+close $fh_roles;
+close $fh_temp_roles;
+
+if ($verbose) {
+    print "Inserting company lsmb roles.";
+}
+if ( system("psql -f $dstdir/sql/temp_lsmb_roles.sql $shell_quiet") ) {
+    die "Unable to insert company lsmb roles.";
+}
+
+if ( system("rm $dstdir/sql/temp_lsmb_roles.sql $shell_quiet") ) {
+    warn "Unable to rm $dstdir/sql/temp_lsmb_roles.sql";
+}
+
+# Disconnect from main database as postgres user
+$dbh->disconnect();
+
+if ($verbose) {
+    print "\nDisconnecting from database $postgres_db\n\n\n";
+}
+
+# Connect to company database as owner of database
+if ($verbose) {
+    print "\nConnecting to company database $company_name\n\n\n";
+}
+
+$dbh = DBI->connect( "DBI:Pg:dbname=$company_name;host=$host;port=$port",
+    $owner, $pass, { 'RaiseError' => 0, pg_enable_utf8 => 1 } );
+
+if ($verbose) {
+    print "\nConnected to database $company_name\n\n\n";
+}
+
+$psql_cmd = "psql -h $host -p $port -U $owner -d $company_name $shell_quiet";
+
+# Load a chart of accounts
+
+if ( $coa ne 'none' ) {
+    open( my $psql_ph, "| $psql_cmd $shell_quiet" )
+        or die "can't run psql: $!";
+    open( my $coa_fh, "<", $coa ) or die "can't open $coa $!";
+    while (<$coa_fh>) {
+        print {$psql_ph} $_;
+        if ($verbose) {
+            print $_;
+        }
+    }
+    close $coa_fh;
+    close $psql_ph;
+}
+
+# Load a gifi
+
+if ($gifi) {
+    open( my $psql_ph, "| $psql_cmd $shell_quiet" )
+        or die "can't run psql: $!";
+    open( my $gifi_fh, "<", $gifi ) or die "can't open $gifi $!";
+    while (<$gifi_fh>) {
+        print {$psql_ph} $_;
+        if ($verbose) {
+            print $_;
+        }
+    }
+    close $gifi_fh;
+    close $psql_ph;
+}
+
+# Add languages
+
+$cmd = "SELECT code FROM language;";
+$sth = $dbh->prepare($cmd);
+$rv  = $sth->execute();
+while ( $language = $sth->fetchrow ) {
+    push @languages, $language;
+}
+
+# Changed to this method instead of COPY to make work from external scripts
+# @locale_language_results was obtained earlier
+for my $result (@locale_language_results) {
+    if ($verbose) {
+        print "lang results = $result";
+    }
+    if ( grep( /$result/, @languages ) ) {
+        next;
+    }
+    ($code, $description) = split /\|/, $result;
+    if ($verbose) {
+        print "additional lang results = $code|$description";
+    }
+
+    $code = $dbh->quote($code);
+    $description = $dbh->quote($description);
+    $dbh->do("INSERT INTO language (code, description) VALUES ($code, $description);");
+}
+
+sub create_ledgersmb_httpd_conf {
+    open( my $tmpl_fh, "<", "$dstdir/ledgersmb-httpd.conf.template" )
+        or die "can't open $dstdir/ledgersmb-httpd.conf.template $!";
+    open( my $lhttpconf_fh, ">", "$httpd_conf_location/ledgersmb-httpd.conf" )
+        or die "can't open $httpd_conf_location/ledgersmb-httpd.conf $!";
+
+    my @tmpl = <$tmpl_fh>;
+    if ($verbose) {
+        print "Creating $httpd_conf_location/ledgersmb-httpd.conf\n";
+    }
+    for my $line (@tmpl) {
+        $line =~ s/WORKING_DIR/$dstdir/g;
+        if ($verbose) {
+            print "$line";
+        }
+        print {$lhttpconf_fh} $line;
+    }
+    close $tmpl_fh;
+    close $lhttpconf_fh;
+    return;
+}
+
+sub setup_ledgersmb_conf {
+
+    # XXX How to deal with this if running from another script?
+    # At this time can have external script ask to directly copy.
+    # External script can edit or allow installer to edit as needed.
+    my $dstdir = shift;
+    print "\n
+        You do not have a copy of ledgersmb.conf.\n
+        Please edit this new copy derived from ledgersmb.conf.default\n";
+    sleep 4;
+    if (system(
+            "cp $dstdir/ledgersmb.conf.default $dstdir/ledgersmb.conf $shell_quiet"
+        )
+        )
+    {
+        die
+            "Unable to cp $dstdir/ledgersmb.conf.default $dstdir/ledgersmb.conf";
+    }
+    if ( system("$editor $dstdir/ledgersmb.conf") ) {
+        die "Unable to edit $dstdir/ledgersmb.conf. Is $editor available?";
+    }
+    return;
+}
+
+sub interactive_setup {
+    print "Welcome to the interactive database and role setup utility.\n\n";
+    print "Name of company to add?  ";
+    chomp( $_ = <ARGV> );
+    if ($_) { $company_name = $_; }
+    if ( !$company_name ) {
+        print $usage;
+        print "\nmissing or empty --company option\n";
+        exit;
+    }
+
+    print "Username of database owner? [$owner]  ";
+    chomp( $_ = <ARGV> );
+    if ($_) { $owner = $_; }
+
+    print "Owner's password? [$pass]  ";
+    chomp( $_ = <ARGV> );
+    if ($_) { $pass = $_; }
+
+    print "Source directory for ledgersmb files? [$srcdir]  ";
+    chomp( $_ = <ARGV> );
+    if ($_) { $srcdir = $_; }
+
+    print "Destination directory for ledgersmb files? [$dstdir]  ";
+    chomp( $_ = <ARGV> );
+    if ($_) { $dstdir = $_; }
+
+    print
+        "Destination directory for ledgersmb-httpd.conf? [$httpd_conf_location]  ";
+    chomp( $_ = <ARGV> );
+    if ($_) { $httpd_conf_location = $_; }
+
+    print
+        "User name or userid that webserver will run ledgersmb as? [$web_server_user]  ";
+    chomp( $_ = <ARGV> );
+    if ($_) { $web_server_user = $_; }
+
+    print
+        "Group name or groupid that webserver will run ledgersmb as? [$web_server_group]  ";
+    chomp( $_ = <ARGV> );
+    if ($_) { $web_server_group = $_; }
+
+    print "host? [$host]  ";
+    chomp( $_ = <ARGV> );
+    if ($_) { $host = $_; }
+
+    print "port? [$port]  ";
+    chomp( $_ = <ARGV> );
+    if ($_) { $port = $_; }
+
+    print "postgres username? [$postgres_username]  ";
+    chomp( $_ = <ARGV> );
+    if ($_) { $postgres_username = $_; }
+
+    print "postgres database name? [$postgres_db]  ";
+    chomp( $_ = <ARGV> );
+    if ($_) { $postgres_db = $_; }
+
+    print
+        "Please enter a $postgres_db password. Type <Enter> for no password.  ";
+    chomp( $_ = <ARGV> );
+    if ($_) {
+        $postgres_password = $_;
+    }
+    else {
+        $no_postgres_pass = 1;
+    }
+
+    print "pgsql extensions contrib directory? [$pgsql_contrib_dir]  ";
+    chomp( $_ = <ARGV> );
+    if ($_) { $pgsql_contrib_dir = $_; }
+
+    if ( !$pgsql_contrib_dir ) {
+        print
+            "\n\nYou must specify a PgSQL extensions contrib directory!\n\n";
+        exit;
+    }
+
+    print "Edit a new ledgersmb.conf from the template? [Y|y]
+       <Enter> will copy the template as-is.  ";
+    chomp( $_ = <ARGV> );
+    if ( $_ !~ /Y|y/ ) { $cp_lsmb_conf_tmpl = 1; }
+
+    #   Copy source files into web site directories
+    make_path($dstdir);
+
+    if ( system("cp -R $srcdir/* $dstdir $shell_quiet") ) {
+        die "unable to cp -R $srcdir/* $dstdir $!\n";
+    }
+
+    chdir $dstdir or die "unable to change to $dstdir $!\n";
+
+    print
+        "List available country codes? Enter [Y|y] to list. Type <Enter> for no.  ";
+    chomp( $_ = <ARGV> );
+    if ( $_ =~ /Y|y/ ) {
+        open( my $fh, "<", "$dstdir/sql/Pg-database.sql" )
+            or die "can't open $dstdir/sql/Pg-database.sql $!";
+        while (<$fh>) {
+            if ( $_
+                =~ /INSERT INTO country\(short_name,name\) VALUES \('(.*)','(.*)'\);/
+                )
+            {
+                print "\n$1  $2";
+            }
+        }
+        close $fh;
+    }
+
+    print "\nCountry code (Two letters)? [$countrycode]  ";
+    chomp( $_ = <ARGV> );
+    if ($_) { $countrycode = $_; }
+
+    print "List available COA's? Enter [Y|y] to list. Type <Enter> for no.  ";
+    chomp( $_ = <ARGV> );
+    if ( $_ =~ /Y|y/ ) {
+        system "ls -1 $dstdir/sql/coa/*/chart/*";
+    }
+
+    print "\n\nChart of Accounts (COA) to use? [$coa]  ";
+    chomp( $_ = <ARGV> );
+    if ($_) { $coa = $_; }
+
+    print "Will you be adding a GIFI? Enter [Y|y]. Type <Enter> for no.  ";
+    chomp( $_ = <ARGV> );
+    if ( $_ =~ /Y|y/ ) {
+        print
+            "List available GIFI's? Enter [Y|y] to list. Type <Enter> for no.  ";
+        chomp( $_ = <ARGV> );
+        if ( $_ =~ /Y|y/ ) {
+            system "ls -1 $dstdir/sql/coa/*/gifi/*";
+        }
+
+        print "\n\nGIFI to use? Type <Enter> for none.  ";
+        chomp( $_ = <ARGV> );
+        if ($_) { $gifi = $_; }
+    }
+
+    print "\nFile Editor? [$editor]  ";
+    chomp( $_ = <ARGV> );
+    if ($_) { $editor = $_; }
+
+    print "Verbose? Enter [Y|y] for verbose. Type <Enter> for no.  ";
+    chomp( $_ = <ARGV> );
+    if ( $_ =~ /Y|y/ ) { $verbose = $_; }
+    if ($verbose) {
+        $shell_quiet = '';
+    }
+
+    if ($verbose) {
+        print "Source Directory = $srcdir\n";
+        print "Web Directory = $dstdir\n";
+        print "Company (and database) Name = $company_name\n";
+        print "Company Database Owner = $owner\n";
+        print "Company Database Owner's Password = $pass\n";
+        print "Host = $host\n";
+        print "Port = $port\n";
+        print "Postgres Username = $postgres_username\n";
+        print "Postgres Password = $postgres_password\n";
+        print "Postgres Database = $postgres_db\n";
+        print "Country Code = $countrycode\n";
+        print "Chart of Accounts = $coa\n";
+
+        if ($gifi) {
+            print "GIFI = $gifi\n";
+        }
+        else {
+            print "GIFI = NONE\n";
+        }
+        print "PgSQL Extensions Directory = $pgsql_contrib_dir\n";
+        print "Web Server Username = $web_server_user\n";
+        print "Web Server Group = $web_server_group\n";
+        print "ledgersmb-httpd.conf Directory = $httpd_conf_location\n";
+    }
+    return;
+}
+
+$dbh->disconnect();
+
+=pod
+
+=head1 NAME
+
+full_files_dbsetup.pl
+
+=head1 VERSION
+
+This documentation refers to full_files_dbsetup.pl version 2.8.0
+
+=head1 SYNOPSIS
+
+Prepares all of standard LedgerSMB installation process.
+
+=head1 DESCRIPTION
+
+Installs web site files from source directory, which can be anywhere,
+and sets chosen owner/group.
+ledgersmb-http.conf is created.
+Chart of Accounts and GIFI if needed.
+Allows select of editor to use for ledgersmb.conf file.
+PostgreSQL needs to be running during installation.
+Script may run verbosely or quiet.
+Works from external scripts interactively, verbosely or silent.
+Works from command line options.
+Defaults to interactive, non-verbose mode.
+
+=head1 BUGS AND LIMITATIONS
+
+This version may fail to work properly due to any future changes to LedgerSMB.
+Please use with caution.
+
+Please report problems to Chris Bennett (chris@bennettconstruction.us)
+
+Patches are welcome.
+
+=head1 AUTHOR
+
+Chris Bennett  (chris@bennettconstruction.us)
+
+=head1 LICENSE AND COPYRIGHT
+
+Copyright (c) 2013 Chris Bennett (chris@bennettconstruction.us).
+
+Permission to use, copy, modify, and distribute this software for any
+purpose with or without fee is hereby granted, provided that the above
+copyright notice and this permission notice appear in all copies.
+
+THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
+WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
+MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
+ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
+WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
+ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
+OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
+
+This script makes use of software developed by the LedgerSMB core team
+and previous sources. The above license only applies to the original work
+present in this script. Any other licenses of the external code still
+applies to that code.
+
+=cut
+
+1;
+
